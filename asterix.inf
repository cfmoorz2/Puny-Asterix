!% -~S
!% $OMIT_UNUSED_ROUTINES=1
!% $ZCODE_LESS_DICT_DATA=1

! The very first lines of the main source code file for a game can
! contain compiler options, like the lines above. -~S disables
! strict error checking. This is otherwise used in z5 and z8 games by
! default. While useful for debugging, it adds ~10 KB to the story file
! size and it makes the game slower.
! $OMIT_UNUSED_ROUTINES=1 makes the compiler remove all routines which
! aren't used. This can save some space.

! When compiling to z8, use the standard library instead
#Iftrue (#version_number == 8);
Constant USEINFORM;
Constant MANUAL_PRONOUNS;
Constant CUSTOM_ABBREVIATIONS;


#Endif;

Constant Story	  "The Candy Striper of St. Asterix";
Constant Headline   "^A Totally Tubular 80s IF Game by Charles Moore, Jr.^";
#Ifv5;
Constant OPTIONAL_PROVIDE_UNDO;
#Endif;
Constant DEATH_MENTION_UNDO;
Constant MAX_CARRIED 40;
Constant OPTIONAL_EXTENDED_METAVERBS;
Constant OPTIONAL_EXTENDED_VERBSET;
!Constant OPTIONAL_GUESS_MISSING_NOUN;
Constant OPTIONAL_PRINT_SCENERY_CONTENTS;
Constant MAX_FLOATING_OBJECTS 55;
Constant MSG_WAIT_DEFAULT "Time passes.";
Constant OPTIONAL_FULL_SCORE 2;
Constant TASKS_PROVIDED;
Constant RUNTIME_ERRORS 0;
Constant NUMBER_TASKS = 17;
Constant MAX_SCORE 160;
Array task_scores -> 10 10 10 10 10 5 5 5 5 5 5 5 5 10 10 10 10 30;
Array UUID_ARRAY string "UUID://8E0FD814-0041-4E2B-B1A1-13E42932A53F//";

Constant MRI_WARNING "~Danger, Strong Magnetic Field~. Below that is a pictograph of a horseshoe magnet
            with intimidating lightning bolts coming out of it. Below that it reads ~This Magnet is Always ON - No loose 
            metal objects. Objects made from or containing ferrous metals should not be taken into this room. Serious 
            injury or property damage may result~.^";
Constant RADIATOR_DESC "It's a cast-iron steam radiator. It's hissing and knocking in a vain struggle against the chill.^";
Constant RADIATOR_TOUCH "I guess third-degree burns are your thing?^";
Constant ELEVATOR_PROSE "The elevator door slides closed. The little light on the card reader turns green and you feel the
    elevator moving beneath you.^";
Constant MAIN_ELEVATOR_PROSE "The elevator doors slide closed. The button lights and you hear mechanical humming from the elevator shaft.^";
Constant SINGLE_BUTTON_PANEL "It's a small panel embedded in the wall next to the elevator doors. It contains a single button.^";
Constant FREDDY_WAKES "Freddy stirs and wakes in a daze.^^~Dude, what happened? Man, did I stroke out grooving to the Aussies?~ 
    He hands back your walkman.^";
Constant NORTHRUP_SPEECH "Of course, now you have no actual evidence that anything at all suspicious has happened here this evening. 
    And, of course, no one is going to take the word of a teenaged candy striper over a respected physician and pillar of the 
    community. And, of course, you will not be geting your letter signed.~^^";
Constant BATHROOM_OCCUPIED "There's a turning bolt on this side of the door with a small red sign reading ~Occupied~.";
Constant BATHROOM_VACANT "There's a turning bolt on this side of the door with a small green sign reading ~Vacant~.";

Attribute valuable;
Attribute encountered;

Release 3;
Serial "221116";

Constant INITIAL_LOCATION_VALUE = b_ward_1;
!Constant INITIAL_LOCATION_VALUE = sub_basement_02;

Constant CUSTOM_PLAYER_OBJECT selfobj;

#Ifndef USEINFORM;

Include "globals.h";
Global dial_setting = 0;
Global elevator_level = 0;
Global elevator_call_level;
Global elevator_active = false;
Global dumbwaiter_level = 0;
Global service_elevator_level = 0;
Global service_elevator_call_level = 2;
Global service_elevator_active = false;
Global SAFE_1;
Global SAFE_2;
Global SAFE_3;
Global SAFE_CURRENT;
Global SAFE_CYCLE 0;
Global walkman_playing = false;

Constant FLAG_COUNT 19;
Constant F_HAVE_FLASHLIGHT 1; ! do you have the flashlight?
Constant F_RETCH_TRIGGERED 2; ! nurse retch has seen the syringe
Constant F_CABINET_UNLOCKED 3; ! has the cabinet handle been appropriately knocked on?
Constant F_SAW_KNOCK_SPOT 4; ! have you seen where retch knocks the door?
Constant F_RETCH_KCL 5; ! nurse retch has seen KCL bottle
Constant F_NORTHRUP_OUT_OF_OFFICE 6; ! northrup is out of the office
Constant F_BADDIES_FOLLOWING 7; !baddies are following at endgame
Constant F_MRI_SUITE_DESTROYED 8; 
Constant F_FREDDY_ASLEEP 9;
Constant F_WALKMAN_BLOCKING 10;
Constant F_HAVE_MET_VIC 11; ! vic has been encountered?
Constant F_HAS_PARSONS_SPOKEN 12; !has ms. parsons given her clue.
Constant F_HAS_SWIPE_CARD 13; !does the player have the  security card
Constant F_SAFE_IS_OPEN 14; !is jorry's safe open
Constant F_HEADPHONES_ARE_UNPLUGGED 15; ! headphones are NOT plugged in
Constant F_FREDDY_WALKMAN_SHY 16; !freddy won't take the walkman a second time
Constant F_FREDDY_HAS_HEARD_THRILLER 17; !he's already heard it
Constant F_SAFE_COMBO_IS_SET 18; ! safe combination has been set
Constant F_SERVICE_ELEVATOR_ON 19; ! is the service elvator working?

Constant MSG_WEAR_DEFAULT 1000;

[ DeathMessage; 
    if (deadflag == 3) print"You are doomed to summer school";
    if (deadflag == 4) print "You survive your injuries but you don't get credit for your volunteerism";
    if (deadflag == 5) print "You smashed the MRI scenner into bits. Now they'll never give you credit for your volunteerism";
    if (deadflag == 6) print "You don't get credit for your volunteering and, sadly, lose your summer vacation to 
        summer school";
    !print "You have lost"; 
];

[LibraryMessages p_msg p_arg_1 p_arg_2;
    p_arg_1 = p_arg_2;
    switch(p_msg) {
    
    MSG_WEAR_DEFAULT:
        if(noun == walkman) "You slip the headphones over your ears. ";
        print_ret "You are now wearing ",(the)noun,".";
    MSG_DISROBE_DEFAULT:
        if (noun == walkman) "You remove the headphones. ";
        "You take off ", (the) noun, ".";
    }
];

[ PrintTaskName task_number;
  switch (task_number) {
      0: "- getting the flashlight";
      1: "- getting the swipe card";
      2: "- finding the syringe";
      3: "- getting the potassium vial";
      4: "- getting the dull steel key";
      5,6,7,8,9,10,11,12: "- getting a signature";
      13: "- finding the green cassette tape";
      14: "- opening the safe";
      15: "- getting the ledger";
      16: "- incapacitating the baddies in the MRI suite";
      17: "- getting Northrup's signature";
  }
];

[ChooseObjects obj code;
    obj = obj;
    if (code == 2)
    {
        if (action == ##wear && obj == walkman) return 0;
        if (action == ##plugin && obj == dummy_headphones) return 2;
        if (action == ##flip && obj in walkman && obj ofclass tape) return 2; 
        if (action == ##push && (IndirectlyContains(player, obj)) == true) return 0;
        if (action == ##push && (~~obj ofclass Button)) return 0;
    }

    if(code < 2)   {
        if(action == ##drop or ##insert or ##puton or ##drop) if(obj notin actor) return 2;
        if(action == ##take or ##insert or ##puton) if (obj has scenery || obj has animate) return 2;
        if(action == ##take or ##insert or ##puton) if (parent(obj) == backpack) return 2;
        !if(action_to_be == ##take) if(obj has scenery || obj in player || obj == player || parent(obj) has container) return 2;
        !if(action_to_be == ##insert) if(obj has scenery || obj in player || obj == player || parent(obj) has container) return 2;
        !if(action_to_be == ##puton) if(obj has scenery || obj in player || obj == player || parent(obj) has container) return 2;  
    }
];

[ GamePreRoutine ;
    if (action == ##give || action == ##show) 
    {
        if (IndirectlyContains(real_location, second) == false)
        {
            print(The)second;
            if (second has pluralname) " aren't here. "; " isn't here. ";
        }
    }
    if(IndirectlyContains(lost, pen)) cant_win();
    if(IndirectlyContains(lost, shrimp) && FlagIsClear(F_HAS_SWIPE_CARD)) cant_win();
    if(IndirectlyContains(lost, syringe) && FlagIsClear(F_RETCH_TRIGGERED)) cant_win();
    if(IndirectlyContains(lost, air_supply_tape) && FlagIsClear(F_SAFE_IS_OPEN)) cant_win();
    if(IndirectlyContains(lost, ledger)) cant_win();

    rfalse;
];

[ cant_win ;
    print"^CAN'T WIN^^";
];

#Ifv5;
[ BeforeParsing x local_parse;
    local_parse = parse;
    for (wn=2 : wn<local_parse->1 : wn++)
    if (buffer->(local_parse->(4*wn + 1)) == '.' &&
    local_parse-->(2*wn - 3) == 'dr' or 'mr' or 'mrs' or 'ms' && local_parse-->(2*wn + 1) == 'chen' or 'johnson' 
        or 'winston' or 'northrup' or 'parsons') {
    buffer->(local_parse->(4*wn + 1)) = ' ';
    x++;
    }
    if (x) @tokenise buffer parse;
    ];
#Endif;

Global following = false;

[ InScope p_actor direction npc k final;
    !if(player in locker_cabinet)
    !    objectloop(obj in female_locker_room) 
    !    {
    !        PlaceInScope(obj);
    !    } 
    objectloop(direction in compass)
    {
        !if (k = location.(direction.door_dir))
        k = real_location.(direction.door_dir);
        if (k == 0) continue;
        if (k has door) final = DoorTo(k); else final = k;
        objectloop (npc in final)
        {
        if (npc ofclass Mover) PlaceInScope(npc);
        } 
    }
    p_actor = 0;
    rfalse;
];

Include "ext_cheap_scenery.h";
Include "ext_talk_menu.h";
Include "puny.h";

[ Initialise r temp;
    intro();
    LookModeLongSub();
    r = random(10);
    SetTime(21 * 60 + r, 1); ! 1:05 am, each turn 5 minutes
    winston.move_mode = WANDER_PATH;
    StartDaemon(winston);
    freddy.move_mode = WANDER_PATH;
    StartDaemon(freddy);
    elliot.move_mode = WANDER_PATH;
    StartDaemon(elliot);
    eugene.move_mode = WANDER_PATH;
    StartDaemon(eugene);
    StartDaemon(nurse_retch);
    player.orders = MyTakeRule;
    !oliver.move_mode = TARGET_PATH;
	!oliver.target_room = conference_room;
    !StartDaemon(oliver);
    move form to player;
    move pen to player;
    move mixtape to backpack;
    move thriller to backpack;
    !init_safe();
    move walkman to backpack;
    move backpack to player;
    give backpack worn;
    SAFE_CURRENT = random(35);
    temp = UUID_ARRAY; !to quiet a compiler warning
];

Include "abbreviations.h";
Include "my_grammar.h";
Include "compass.h";
Include "pathfinder.h";
Include "rooms.h";
Include "conversations.h";
Include "npcs.h";
Include "items.h";
Include "elevator.h";
Include "dumbwaiter.h";
Include "service_elevator.h";
Include "ext_flags.h";


Object master_timer
    with daemon [;
        if (the_time == 420) "GAME OVER";
];

Object selfobj "you"
	with
		name 'me' 'myself' 'self',
		short_name  "yourself",
		description "You're a seventeen-year-old teenage girl. You're wearing a red-and-white pinstriped 
        candy striper uniform and your favorite black-and-white checkerboard Vans. ",
		before NULL,
		after NULL,
		life NULL,
		each_turn NULL,
		time_out NULL,
		describe NULL,
		add_to_scope 0,
		capacity MAX_CARRIED,
		parse_name 0,
		orders 0,
		number 0,
	has concealed animate proper transparent;

#Endif;

#Ifdef USEINFORM;

Include "Parser";
Include "VerbLib";

#Endif;

! ============================================================================ !

[open_or_closed door;
    if (door has open) print "open"; else print "closed";
];

[ freddy_wakes_up ;
    print(string)FREDDY_WAKES;
    ClearFlag(F_FREDDY_ASLEEP);
    SetFlag(F_FREDDY_WALKMAN_SHY);
    move walkman to player;
    scope_modified = true;
    StartDaemon(freddy);
];

!!! THE SIGNATURE ARRAY HAS 17 SLOTS TO MATCH THE ACHIEVEMENT ARRAY. 5-11 ARE DEVOTED TO SIGNATURES
[ signature_count  x y i;
    for (x=5 : x < 13: x++)
    {
        i = signatures-->x;
        !print"here i = ",(name)i,"^";
        if (i ofclass MyNPC) y++;
    }
    return y;
];

[ show_signatures x i k;
    for (x=5 : x<13: x++)
    {
        i = signatures-->x;
        k = signatures-->(x+1);
        if (k==0) { print(name)i; rtrue;}
        print(name)i,", ";
    }
];

[ check_signature npc x i;
    for (x=5 : x < 13: x++)
    {
        i = signatures-->x;
        if (i == npc) { print(The)npc," has already signed your form.^"; return true; }
    }
    return false;
];

[ add_signature npc x i o; 
    x = signature_count();
    if (x == 8) "Your signature form is complete. ";
    if (pen notin player) "You need a pen. ";
    if (check_signature(npc) == true) rtrue;
    for (i=5 : i < 13: i++)
    {
        o = signatures-->i;
        if (o == 0) 
        {
            if (npc.signature_desc()) signatures-->i = npc;
            Achieved(i);
            rtrue;
        }
    }
];

[MyTakeRule count i x ;
    if (action == ##go && location == thedark)
    {
        if (real_location == ceiling_01 && selected_direction == d_to) rfalse;
            "It's impossible to crawl around up here in the dark. ";
    }
    if (action ~= ##take) rfalse;
    if (~~noun provides mass) rfalse;
    count = 0;
    x = noun.mass;
    objectloop(i in player && i hasnt worn) {
        count = count + i.mass;
    }
    !print"carrying = ",count,"^";
    !print"mass = ",x,"^";
    if((count + x) > MAX_CARRIED) "That's too much for you to carry. ";
    rfalse;
];

[ intro;
    "One more night. One more cold winter night. It's been a painful four weeks but you're SO close you can taste it. And then 
    Mr. Rogers, your Ethics teacher can suck it.^^
    You're going to be a senior and you have BIG plans coming up this summer. Tiffany is going to be life-guarding at the pool 
    and she's promised to get you a cushy job at the snack bar. You'll spend your working days surrounded by cute boy lifeguards and your 
    off-time at the mall.^^
    But then your grades started to slip and, inconceivably, the topic of summer school has come up. To avoid this at all cost, 
    you took Ethics 101 this semester, an easy ~A~. But, in order to pass, you have to perform some sort of volunteer activity for four long
    painful weeks. 
    The concept of mandatory volunteerism seems odd to you but, nonetheless, here you are, working as a candy striper at St. Asterix 
    Hospital, the oldest and most run-down hospital in the city.^^
    It hasn't been too bad. You mainly push the book cart around so patients can pick out a book or magazine. Tonight's your last night and 
    your sponsor at the hospital, Mr. Jorry, was going to give you a letter tonight confirming your volunteer hours after you also get 
    the signatures of 8 patients and staff. But rudely, Mr. Jorry has been found dead in his office. And, even worse, a 
    blizzard blew in tonight and now it doesn't seem like you can even get home.^^
    Mr. Jorry may have been killed and the killer may still be in the building, trapped inside by the storm. But, more importantly, 
    you are NOT going to summer school this year and you'll unmask a murderer if you have to in order to get those signatures and 
    that letter. ";
];   

[read_news i;
    i = random(12);
    switch(i)   {
        1: "You tear up a little as you read about and recall the series finale of ~M*A*S*H~. ";
        2: "You notice something called ~The Disney Channel~ hitting cable TV. ";
        3: "You see a review of ~The Day After~, that depressing TV movie your history teacher made you watch. ";
        4: "You see a bunch of stupid ads for toys from your little brother's favorite show, ~He-Man and the Masters of the Universe~. ";
        5: "There's a pictorial from the movie ~WarGames~. Matthew Broderick is dreamy. ";
        6: "You notice a very short notice about somthing called ~ARPANET~ starting to use something called 
	        ~Internet Protocol~. ";
        7: "You see a review of a game called ~Planetfall~ from a strange-sounding company called ~Infocom~. ";
        8: "There's a pictorial from the Knoxville World's fair. The big shiny orange ball catches your eye. ";
        9: "You flip through an interview with Steven Spielberg. You bawled your eyes out when E.T. died. ";
        10: "There's an article about something called a ~compact disc~. They'll have to pry your 45s from your 
	        cold dead hands. ";
        11: "You see a blurb about ~Thriller~ selling yet another million copies. ";
        12: "There's a mention of a game called ~Deadline~. Sounds boring; you hate mysteries. ";
    }
];  

[ init_safe ;
    SAFE_1 = random(35);
    SAFE_2 = random(35);
    SAFE_3 = random(35);
    SAFE_CURRENT = random(35);
    !SAFE_1 = 1;
    !SAFE_2 = 2;
    !SAFE_3 = 3;
    "^combo = ",SAFE_1,", ",SAFE_2,", ",SAFE_3,".^";
];

[ DialPhone ;
    print"You dial the phone...^";
    print (string) random(
    "You seem to have reached the local weather line. You learn that there's a blizzard outside. ",
    "You seem to have reached a local movie theater. You are pleased to hear that ~Doctor Detroit~ will be playing 
    tomorrow at 3:30, 6:15, 9:24, and 11:14pm. ",
    "You seem to have reached a pizza parlor one town over. The person on the other end snippily informs you that they are not currently 
    delivering due to the blizzard conditions. He also asks if you are insane. ",
    "You seem to have reached an adult-oriented phone chat service. While somewhat curious, you, unfortunately, don't have a
    credit card yet. ",
    "You hear a recording on the other end. You have reached the ~Lapsed Amish~ support line. ");
];
Object follow_timer 
    with
        time_left,
        time_out [;
            move trio to hallway_m2;
            trio.move_mode = FOLLOW_PATH;
            StartDaemon(trio);
        ];

Object baddie_mri_delay
    with   
        time_left, 
        time_out [;
            if (player in mri_compartment)
                print"Suddenly, the hatch swings open and you look up to see the snarling grin of Vic. He pull you 
                up into the MRI suite.^^";
            if (ledger in player)
                {
                    print"Vic rips the ledger from your hands and passes it to Dr. Northrup.^^
                    ~Thank you, my dear, for delivering this to us. ";
                } else {
                    print"Northrup begins. ~Trust me, my dear, we'll find the ledger. ";
                }
            print(string)NORTHRUP_SPEECH;
            deadflag = 3;
            rtrue;   
        ];

[ mri_no_cart ;
    if (real_location == mri_anteroom or mri_scanner)
         "^^Suddenly, you hear a loud ~clunk~ from within the walls and the mechanical whining from the MRI scanner becomes a loud whirring
        screech. The noise continues for a moment and then starts to wind down in pitch and intensity. In another moment, 
        it's back to a mechanical ~chirping~ sound.^";
    
];

[ mri_cart_anteroom; 
    print"^Suddenly, you hear a loud ~clunk~ from within the walls and the mechanical whining from the MRI scanner becomes a loud whirring
    screech. Through the window you see the metal cart and the oxygen cannisters on it start to shake violently. As the 
    noise crescendos, the cannisters take flight and dart though the air toward and around the MRI scanner itself, each one a small 
    missile bashing into the white tube and smashing the safety glass of the door and window. With a pained moan, the scanner
    winds down and stops and the room is eerily silent.^^You look over the carnage of the very expensive MRI machine, 
    now shattered and in pieces and your heart sinks as you realize that you'll never get credit for your volunteer time now.^";
    deadflag = 3;
    rtrue;
];

[ mri_cart_hatch_closed;
    ! cart in room, player in hatch, hatch closed
    if (trio in mri_scanner) 
    {
        print"^Suddenly, you hear a loud ~clunk~ and the mechanical whining from the walls surrounding you becomes a loud screech. 
        Above you, you hear what sounds like metallic clattering followed by loud crashing and glass breaking. You also hear screaming and 
        the dull ~thump~ of hard metallic things smacking into soft fleshy things. After a moment, there's a pained moan from the 
        MRI scanner and the screeching winds down and the you hear only eerie silence.^";
        SetFlag(F_MRI_SUITE_DESTROYED);
        remove trio;
        StopDaemon(trio);
        move injured_trio to mri_scanner;
        move letter to mri_scanner;
        StopTimer(baddie_mri_delay);
        Achieved(16);
        rtrue; 
    }
    print"^Suddenly, you hear a loud ~clunk~ from within the walls around you and the mechanical whining from the MRI 
    scanner becomes a loud whirring screech. Above you, you hear what sounds like metallic clattering followed by loud 
    crashing and glass breaking. After a moment, there's a pained moan from the MRI scanner and the screeching winds 
    down and the you hear only eerie silence.^^You peek out of the compartment and see the very expensive MRI machine 
    shattered and in pieces and your heart sinks as you realize that you'll never get credit for 
    your volunteer time now.^";
    deadflag = 3;
    rtrue;
];

[ mri_cart_hatch_open;
    ! cart in room, player in hatch, hatch closed
    if (trio in mri_scanner) 
    {
        print"^Suddenly, you hear a loud ~clunk~ and the mechanical whining from the surrounding walls becomes a loud screech. 
        The metal cart and the oxygen cannisters on it start to shake violently. As the noise crescendos, the cannisters 
        take flight and dart though the air toward and around the MRI scanner itself, each one a small 
        missile bashing into the white tube and smashing the safety glass of the door and window. Keeping your wits about you, 
        you duck down into the compartment, shutting the hatch behind you. Above you, you hear screaming and 
        the dull ~thump~ of hard metallic things smacking into soft fleshy things. After a moment, there's a pained moan from the 
        MRI scanner and the screeching winds down and then you hear only eerie silence.^";
        SetFlag(F_MRI_SUITE_DESTROYED);
        give mri_compartment ~open;
        remove trio;
        StopDaemon(trio);
        StopTimer(baddie_mri_delay);
        Achieved(16);
        move injured_trio to mri_scanner;
        move letter to mri_scanner;
        rtrue; 
    }
    print"^Suddenly, you hear a loud ~clunk~ from within the walls around you and the mechanical whining from the MRI 
    scanner becomes a loud whirring screech. The metal cart and the oxygen cannisters on it start to shake violently. As the 
    noise crescendos, the cannisters take flight and dart though the air toward and around the MRI scanner itself, each one a small 
    missile bashing into the white tube and smashing the safety glass of the door and window. Keeping your wits about you, 
    you duck down into the compartment, shutting the hatch behind you. After a moment, there's a pained moan from the MRI 
    scanner and the screeching winds down and the you hear only eerie silence.^^You peek out of the compartment and see
    the very expensive MRI machine shattered and in pieces and your heart sinks as you realize that you'll never get credit for 
    your volunteer time now.^";
    deadflag = 3;
    rtrue;
];

[ mri_cart_unprotected ; 
    if (trio in mri_scanner)
    {
        print"^^Suddenly, you hear a loud ~clunk~ from within the walls and the mechanical whining from the MRI scanner becomes a loud whirring
        screech. You notice the metal cart and the oxygen cannisters on it start to shake violently. As the 
        noise crescendos, the cannisters take flight and dart though the air toward and around the MRI scanner itself, each one a small 
        missile bashing into the white tube and smashing the safety glass of the door and window.^^
        You, Ratchet, Northrup, and Vic try to shield yourselves but to no purpose as the flying debris slams into everyone.^^
        Finally, the machine groans painfully to a halt, leaving you and your pursuers moaning on the floor.^^
        Propitiously, however, the police have finally made it through the blizzard and have arrived to survey the scene. The good news is 
        that you have unmasked the murderers to the thanks and gratitude of all. The bad news is that you are in no state to go back to 
        school after your run-in with the MRI machine. You miss just enough classroom days that you are required to attend summer
        school, despite your volunteer work.^";
        deadflag = 3;
        rtrue;
    }
    print"^^Suddenly, you hear a loud ~clunk~ from within the walls and the mechanical whining from the MRI scanner becomes a loud whirring
    screech. You notice the metal cart and the oxygen cannisters on it start to shake violently. As the 
    noise crescendos, the cannisters take flight and dart though the air toward and around the MRI scanner itself, each one a small 
    missile bashing into the white tube and smashing the safety glass of the door and window.^^
    You raise your arms in an effort to shield yourself but you are pummeled by the flying debris, knocking you unconscious. 
    With a pained moan, the scanner winds down and stops and the room is eerily silent.^^
    You come to and groggily look around. Seeing the very expensive MRI machine shattered and in pieces your heart sinks as 
    you realize that you'll never get credit for your volunteer time now.^";
    deadflag = 3;
    rtrue;
];
        
#Ifdef USEINFORM;
Include "Grammar";
#Endif;

!Verb 'hang'	 * held 'on' noun	-> PutOn;

! ============================================================================ !
